X := SimplifiedModel(X0NQuotient(171,[9,19,171]));
pts := SetToSequence(Points(X : Bound := 100));
bp := pts[1];
J := Jacobian(X);
ptsJ := [P-bp : P in pts[2..#pts]];
ptsJ := [P : P in ptsJ | Order(P) eq 0];
assert #ptsJ ne 0;

// Since the Mordell-Weil rank of the Jacobian J of X_0(171)^* is known to be 1,
// this verifies that differences of rational points on the curve X_0(171)^*
// generate a finite index subgroup of J.

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

X := SimplifiedModel(X0NQuotient(176,[11,16,176]));
pts := SetToSequence(Points(X : Bound := 100));
bp := pts[1];
J := Jacobian(X);
ptsJ := [P-bp : P in pts[2..#pts]];
ptsJ := [P : P in ptsJ | Order(P) eq 0];
assert #ptsJ ne 0;

// Since the Mordell-Weil rank of the Jacobian J of X_0(176)^* is known to be 1,
// this verifies that differences of rational points on the curve X_0(176)^*
// generate a finite index subgroup of J.

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// The Mordell-Weil rank of the Jacobian J of X_0(279)^* is 2.
// So it is a little tricky to verify that differences of rational points on the curve
// generate a finite index subgroup of J.

X := SimplifiedModel(X0NQuotient(279,[9,31,279]));
pts := SetToSequence(Points(X : Bound := 100));
bp := pts[1];
J := Jacobian(X);
ptsJ := [P-bp : P in pts[2..#pts]];
ptsJ := [P : P in ptsJ | Order(P) eq 0];

D1 := ptsJ[1];
for i := 2 to #ptsJ do
    if Order(D1 + ptsJ[i]) eq 0 then
    	D2 := ptsJ[i];
    	break;
    end if;
end for;
D1;
D2;

// The following shows that the only torsion in the Mordell-Weil group of J over Q is
// 2-torsion

cond := Conductor(X);
assert cond mod 19 ne 0;
assert cond mod 23 ne 0;
Factorisation(#(Jacobian(ChangeRing(X,GF(19)))));
Factorisation(#(Jacobian(ChangeRing(X,GF(23)))));


// The structure of J(F_41) and the reduction of D1 and D2 mod 41 are printed by the code below.
// It shows that the reduction of the group generated by D1 and D2 mod 41 surjects onto
// J(F_41)/3*J(F_41) = Z/3 x Z/3

cond := Conductor(X);
p := 41;
assert cond mod p ne 0;

Xp := ChangeRing(X,GF(p));
P3_p<u,v,w> := CoordinateRing(AmbientSpace(Xp));
Dinfp := Divisor(Xp,ideal<P3_p | w>);
assert Degree(Dinfp) eq 2;
f1 := u*w;
g1 := v - (u^6-w^6);
D1p := Divisor(Xp,ideal<P3_p | f1,g1>);
assert Degree(D1p) eq 2;
f2 := u*w-w^2;
g2 := v - (u^6-4*w^6);
D2p := Divisor(Xp,ideal<P3_p | f2,g2>);
assert Degree(D2p) eq 2;

PicXp, phi, psi := ClassGroup(Xp);
JFp := TorsionSubgroup(PicXp);
divsp := [psi(D1p-Dinfp), psi(D2p-Dinfp)];
print JFp, divsp;

mat := Matrix(Integers(),2,2,[Eltseq(JFp ! D) : D in divsp]);
ChangeRing(mat,GF(3));
assert Determinant(ChangeRing(mat,GF(3))) ne 0;

// This finishes the verification that differences of rational points on the curve X_0(279)^*
// generate a finite index subgroup of its Jacobian.

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

